
% Porgram with qualified name QN is defined in source File between lines First and Last.
program_source(QN, File, First, Last) :-
        program(_, _, QN, Begin, End),
        annotation(Begin, Source, First, _, _, _),
        annotation(End, Source, Last, _, _, _),
        extract_source(Source, File).

% Program P is a subprogram of another program (workflow) W.
subprogram(P) :-
    program(P, _, _, _, _),
    program(W, _, _, _, _),
    has_subprogram(W, P).

% Workflow W is the top-level workflow.
top_workflow(W) :-
    workflow(W),
    not subprogram(W).

% Port P reads or writes data D with name N and qualified name QN.
port_data(P, N, QN) :-
    port_connects_to_channel(P, C),
    channel(C, D),
    data(D, N, QN).

% Port P is an input for data D.
data_in_port(P, D) :-
    port_connects_to_channel(P, C),
    channel(C, D),
    has_in_port(_, P).

% Program P1 is immediately downstream of Program P2.
program_immediately_downstream(P1, P2) :-
    has_in_port(P1, In),
    port_connects_to_channel(In, C),
    port_connects_to_channel(Out, C),
    has_out_port(P2, Out).
 
% Program P1 is downstream of Program P2.
program_downstream(P1, P2) :-
    program_immediately_downstream(P1, P2).
program_downstream(P1, P2) :-
    program_downstream(P1, P),
    program_immediately_downstream(P, P2).

% Program P1 is immediately upstream of Program P2.
program_immediately_upstream(P1, P2) :-
    program_immediately_downstream(P2, P1).

% Program P1 is upstream of Program P2.
program_upstream(P1, P2) :-
    program_downstream(P2, P1).
    
% Data D1 is immediately downstream of data D2.
data_immediately_downstream(D1, D2) :-
    channel(C2, D2),
    port_connects_to_channel(In, C2),
    has_in_port(P, In),
    has_out_port(P, Out),
    port_connects_to_channel(Out, C1),
    channel(C1, D1).

% Data D1 is immediately upstream of data D2.
data_immediately_upstream(D1, D2) :-
    data_immediately_downstream(D2, D1).

% Data D1 is downstream of data D2.
data_downstream(D1, D2) :-
    data_immediately_downstream(D1, D2).
data_downstream(D1, D2):-
    data_immediately_downstream(D, D2),
    data_downstream(D1, D).

% Data D2 is upstream of Data D1
data_upstream(D1, D2) :-
    data_downstream(D1, D2).

% Resource R1 is upstream of resource R2.
resource_upstream(R1, R2) :-
    data_resource(D1, R1),
    data_resource(D2, R2),
    data_upstream(D1, D2).

% Data D with URI variable U passed through output port P.
data_output_port_has_uri_variable(D, U) :-
    channel(C, D),
    port_connects_to_channel(P, C),
    has_out_port(_, P),
    uri_variable(U, _, P).   

% Data D with URI variable U passed through input port P.
data_input_port_has_uri_variable(D, U) :-
    channel(C, D),
    port_connects_to_channel(P, C),
    has_in_port(_, P),
    uri_variable(U, _, P).

% Resource R with uri variable U named N with value V passed through Port P.
resource_metadata(R, N, V, P) :-
    data_resource(D, R),
    channel(C, D),
    port_connects_to_channel(P, C),
    uri_variable(U, N, P),
    uri_variable_value(R, U, V).

% Resource R was read with a metadata variable named N with value V.
read_resource_metadata(R, N, V) :-
    resource_metadata(R, N, V, P),
    has_in_port(_, P).

% Resource R was written with a metadata variable named N with value V.
written_resource_metadata(R, N, V) :-
    resource_metadata(R, N, V, P),
    has_out_port(_, P).

% Resources R1 and R2 have metadata variables with shared name N.
common_metadata_variable(R1, R2) :-
    resource_metadata(R1, N, _, P1),
    resource_metadata(R2, N, _, P2).

% Resources R1 and R2 have metadata variables with shared name N but different values V1 and V2.
common_metadata_values_differ(R1, R2) :-
    resource_metadata(R1, N, V1, _),
    resource_metadata(R2, N, V2, _),
    V1 != V2.

% Resource R1 depends on resource R2.
depends_on(R1, R2) :-
    common_metadata_variable(R1, R2),
    not common_metadata_values_differ(R1, R2),
    resource_upstream(R1, R2),
    R1 != R2.
     